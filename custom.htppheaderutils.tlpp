#include 'totvs.ch'
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

class HttpHeaderTools
    data cCompany as character
    data cBranch  as character

    public method new() constructor
    public method validateTenant() as array
    public method getCompany() as character
    public method getBranch()  as character
endclass

method new() class HttpHeaderTools
    ::cCompany := ""
    ::cBranch  := ""
Return self

method getCompany() class HttpHeaderTools
Return ::cCompany

method getBranch() class HttpHeaderTools
Return ::cBranch

method validateTenant() class HttpHeaderTools
    Local aReturn         as array
    Local oHeaderValue    as json
    Local aCompanyAndBranch as array
    Local cCompany        as character
    Local cBranch         as character
    Local cHeaderParam    as character
    Local cHeaderTenant   as character
    Local lHasHeaderKey   as logical

    cHeaderParam  := SuperGetMV("MV_XCHKHDR", .F., "tenantid")
    lHasHeaderKey := oRest:existKeyHeaderRequest(cHeaderParam)

    if .Not. lHasHeaderKey
        aReturn := { .F., "The header '" + cHeaderParam + "' was not provided.", 400 }
        Return aReturn
    endif

    oHeaderValue := oRest:getHeaderRequest()
    if oHeaderValue == Nil
        aReturn := { .F., "Invalid headers: request headers missing.", 400 }
        Return aReturn
    endif

    cHeaderTenant := AllTrim(oHeaderValue[cHeaderParam])
    if Empty(cHeaderTenant)
        aReturn := { .F., "The header is missing or empty.", 400 }
        Return aReturn
    endif

    aCompanyAndBranch := StrTokArr(cHeaderTenant, ",")
    if ValType(aCompanyAndBranch) != "A" .Or. Len(aCompanyAndBranch) < 2
        aReturn := { .F., "Invalid header format. Expected 'company,branch'", 400 }
        Return aReturn
    endif

    cCompany := AllTrim(aCompanyAndBranch[1])
    cBranch  := AllTrim(aCompanyAndBranch[2])

    if !FwFilExist(cCompany, cBranch)
        aReturn := { .F., "Company and/or Branch provided in the header is invalid or does not exist.", 422 }
        Return aReturn
    endif

    ::cCompany := cCompany
    ::cBranch  := cBranch
    aReturn    := { .T., "", 0 }

Return aReturn
