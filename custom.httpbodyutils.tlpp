#include 'totvs.ch'
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Class HttpBodyUtils
    public data oJsonBody as object

    public method new() constructor
    public method validateBody() as array
endclass

method new() class HttpBodyUtils
    ::oJsonBody := Nil
Return self

method validateBody() class HttpBodyUtils
    Local aReturn    as array
    Local cBody      as character
    Local vJsonError as variant

    cBody := oRest:getBodyRequest()

    if Empty(cBody)
        aReturn := { .F., "Request body not sent.", 400 }
        Return aReturn
    endif

    ::oJsonBody  := JsonObject():new()
    vJsonError   := ::oJsonBody:FromJson(cBody)

    if vJsonError != Nil
        cBody      := DecodeUTF8(cBody)
        vJsonError := ::oJsonBody:FromJson(cBody)
    endif

    if vJsonError != Nil
        aReturn := { .F., "Invalid JSON format: " + vJsonError, 400 }
        Return aReturn
    endif

    aReturn := { .T., "", 0 }
Return aReturn
